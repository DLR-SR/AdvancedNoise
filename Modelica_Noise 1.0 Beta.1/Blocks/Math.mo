within Modelica_Noise.Blocks;
package Math "Library of mathematical blocks"
  extends Modelica.Icons.Package;

  block ContinuousMean
    "Calculates the empirical expectation (mean) value of its input signal"
    extends Modelica.Blocks.Interfaces.BlockIcon;
    parameter Modelica.SIunits.Time t_eps(min=0.0)=1e-7
      "Mean value calculation starts at startTime + t_eps"
      annotation(Dialog(group="Advanced"));

    Modelica.Blocks.Interfaces.RealInput u "Noisy input signal" annotation (Placement(transformation(extent={{-140,-20},{-100,20}})));
    Modelica.Blocks.Interfaces.RealOutput y
      "Expectation (mean) value of the input signal"
      annotation (Placement(transformation(extent={{100,-10},{120,10}})));

  protected
    Real mu "Internal integrator variable";
    parameter Real t_0(fixed=false) "Start time";
  initial equation
    t_0 = time;
    mu  = u;
  equation
    der(mu) = noEvent(if time >= t_0 + t_eps then (u-mu)/(time-t_0) else 0);
    y       = noEvent(if time >= t_0 + t_eps then mu                else u);

    annotation (Documentation(revisions="<html>
<p>
<table border=1 cellspacing=0 cellpadding=2>
<tr><th>Date</th> <th align=\"left\">Description</th></tr>

<tr><td valign=\"top\"> June 22, 2015 </td>
    <td valign=\"top\"> 

<table border=0>
<tr><td valign=\"top\">
         <img src=\"modelica://Modelica_Noise/Resources/Images/Blocks/Noise/dlr_logo.png\">
</td><td valign=\"bottom\"> 
         Initial version implemented by
         A. Kl&ouml;ckner, F. v.d. Linden, D. Zimmer, M. Otter.<br>
         <a href=\"http://www.dlr.de/rmc/sr/en\">DLR Institute of System Dynamics and Control</a>
</td></tr></table>
</td></tr>

</table>
</p>
</html>",                                   info="<html>
<p>This block continuously calculates the mean value of its input signal. It uses the function:</p>
<blockquote>
<pre>    integral( u over time)
y = ----------------------
      time - startTime</pre>
</blockquote>
<p>This can be used to determine the empirical expectation value of a random signal, such as generated by the <a href=\"Blocks.Noise\">Noise</a> blocks.</p>
<p>The parameter t_eps is used to guard against division by zero (the mean value computation
starts at startTime + t_eps and before that time instant y = u).</p>
<p>See also the <a href=\"Modelica.Blocks.Math.Mean\">Mean</a> block for a sampled implementation.</p>

<p>
This block is demonstrated in the examples
<a href=\"modelica://Modelica_Noise.Blocks.Examples.NoiseExamples.UniformNoiseProperties\">UniformNoiseProperties</a>,
<a href=\"modelica://Modelica_Noise.Blocks.Examples.NoiseExamples.NormalNoiseProperties\">NormalNoiseProperties</a> and
<a href=\"modelica://Modelica_Noise.Blocks.Examples.NoiseExamples.WeibullNoiseProperties\">WeibullNoiseProperties</a>.
</p>
</html>"),                                   Icon(coordinateSystem(
            preserveAspectRatio=false, extent={{-100,-100},{100,100}}),
          graphics={
          Polygon(
            points={{94,0},{72,8},{72,-8},{94,0}},
            lineColor={192,192,192},
            fillColor={192,192,192},
            fillPattern=FillPattern.Solid),
          Line(points={{-86,0},{72,0}}, color={192,192,192}),
          Line(points={{-76,68},{-76,-80}}, color={192,192,192}),
          Polygon(
            points={{-76,90},{-84,68},{-68,68},{-76,90}},
            lineColor={192,192,192},
            fillColor={192,192,192},
            fillPattern=FillPattern.Solid),
          Line(
             points={{-76,-31},{-62,-31},{-62,-15},{-54,-15},{-54,-63},{-46,-63},
                {-46,-41},{-38,-41},{-38,43},{-30,43},{-30,11},{-30,11},{-30,-49},
                {-20,-49},{-20,-31},{-10,-31},{-10,-59},{0,-59},{0,23},{6,23},{6,
                37},{12,37},{12,-19},{22,-19},{22,-7},{28,-7},{28,-37},{38,-37},
                {38,35},{48,35},{48,1},{56,1},{56,-65},{66,-65}},
              color={215,215,215}),
          Line(
            points={{-76,-24},{70,-24}},
            color={0,0,0},
            smooth=Smooth.None)}));
  end ContinuousMean;

  block Variance "Calculates the empirical variance of its input signal"
    extends Modelica.Blocks.Interfaces.BlockIcon;
    parameter Modelica.SIunits.Time t_eps(min=0.0)=1e-7
      "Variance calculation starts at startTime + t_eps"
      annotation(Dialog(group="Advanced"));

    Modelica.Blocks.Interfaces.RealInput u "Noisy input signal" annotation (Placement(transformation(extent={{-140,-20},{-100,20}})));
    Modelica.Blocks.Interfaces.RealOutput y "Variance of the input signal"
      annotation (Placement(transformation(extent={{100,-10},{120,10}})));
  protected
    Real mu "Mean value (state variable)";
    Real var "Variance (state variable)";
    parameter Real t_0(fixed=false) "Start time";
  initial equation
    t_0 = time;
    mu  = u;
    var = 0;
  equation
    der(mu)  = noEvent(if time >= t_0 + t_eps then (u-mu)/(time-t_0)             else 0);
    der(var) = noEvent(if time >= t_0 + t_eps then ((u-mu)^2 - var)/(time - t_0) else 0);
    y        = noEvent(if time >= t_0 + t_eps then max(var,0)                    else 0);

    annotation (Documentation(revisions="<html>
<p>
<table border=1 cellspacing=0 cellpadding=2>
<tr><th>Date</th> <th align=\"left\">Description</th></tr>

<tr><td valign=\"top\"> June 22, 2015 </td>
    <td valign=\"top\"> 

<table border=0>
<tr><td valign=\"top\">
         <img src=\"modelica://Modelica_Noise/Resources/Images/Blocks/Noise/dlr_logo.png\">
</td><td valign=\"bottom\"> 
         Initial version implemented by
         A. Kl&ouml;ckner, F. v.d. Linden, D. Zimmer, M. Otter.<br>
         <a href=\"http://www.dlr.de/rmc/sr/en\">DLR Institute of System Dynamics and Control</a>
</td></tr></table>
</td></tr>

</table>
</p>
</html>",                                   info="<html>
<p>
This block calculates the empirical variance of its input signal. It is based on the formula
(but implemented in a more reliable numerical way):
</p>
<blockquote>
<pre>y = mean(  (u - mean(u))^2  )</pre>
</blockquote>

<p>The parameter t_eps is used to guard against division by zero (the variance computation
starts at startTime + t_eps and before that time instant y = 0).</p>
<p>The variance of a signal is also equal to its mean power.</p>

<p>
This block is demonstrated in the examples
<a href=\"modelica://Modelica_Noise.Blocks.Examples.NoiseExamples.UniformNoiseProperties\">UniformNoiseProperties</a>,
<a href=\"modelica://Modelica_Noise.Blocks.Examples.NoiseExamples.NormalNoiseProperties\">NormalNoiseProperties</a> and
<a href=\"modelica://Modelica_Noise.Blocks.Examples.NoiseExamples.WeibullNoiseProperties\">WeibullNoiseProperties</a>.
</p>
</html>"),
      Diagram(coordinateSystem(preserveAspectRatio=false, extent={{-100,-100},{100,
              100}}), graphics),
      Icon(coordinateSystem(preserveAspectRatio=false, extent={{-100,-100},{100,
              100}}), graphics={
          Line(points={{-76,68},{-76,-80}}, color={192,192,192}),
          Line(points={{-86,0},{72,0}}, color={192,192,192}),
          Line(
             points={{-76,-13},{-62,-13},{-62,3},{-54,3},{-54,-45},{-46,-45},{-46,
                -23},{-38,-23},{-38,61},{-30,61},{-30,29},{-30,29},{-30,-31},{-20,
                -31},{-20,-13},{-10,-13},{-10,-41},{0,-41},{0,41},{6,41},{6,55},
                {12,55},{12,-1},{22,-1},{22,11},{28,11},{28,-19},{38,-19},{38,53},
                {48,53},{48,19},{56,19},{56,-47},{66,-47}},
              color={215,215,215}),
          Polygon(
            points={{94,0},{72,8},{72,-8},{94,0}},
            lineColor={192,192,192},
            fillColor={192,192,192},
            fillPattern=FillPattern.Solid),
          Line(
            points={{-76,66},{70,66}},
            color={215,215,215},
            smooth=Smooth.None),
          Line(
            points={{-16,0},{-16,48}},
            color={0,0,0},
            smooth=Smooth.None),
          Polygon(
            points={{-76,90},{-84,68},{-68,68},{-76,90}},
            lineColor={192,192,192},
            fillColor={192,192,192},
            fillPattern=FillPattern.Solid),
          Polygon(
            points={{-16,66},{-24,44},{-8,44},{-16,66}},
            lineColor={0,0,0},
            fillColor={0,0,0},
            fillPattern=FillPattern.Solid)}));
  end Variance;

  block StandardDeviation
    "Calculates the empirical standard deviation of its input signal"
    extends Modelica.Blocks.Interfaces.BlockIcon;
    parameter Modelica.SIunits.Time t_eps(min=0.0)=1e-7
      "Standard deviation calculation starts at startTime + t_eps"
      annotation(Dialog(group="Advanced"));

    Modelica.Blocks.Interfaces.RealInput u "Noisy input signal" annotation (Placement(transformation(extent={{-140,-20},{-100,20}})));
    Modelica.Blocks.Interfaces.RealOutput y
      "Standard deviation of the input signal"
      annotation (Placement(transformation(extent={{100,-10},{120,10}})));

    Variance variance(t_eps=t_eps)
      annotation (Placement(transformation(extent={{-60,-10},{-40,10}})));
    Modelica.Blocks.Math.Sqrt sqrt1
      annotation (Placement(transformation(extent={{-20,-10},{0,10}})));
  equation
    connect(variance.u, u) annotation (Line(
        points={{-62,0},{-120,0}},
        color={0,0,127},
        smooth=Smooth.None));
    connect(sqrt1.u, variance.y) annotation (Line(
        points={{-22,0},{-39,0}},
        color={0,0,127},
        smooth=Smooth.None));
    connect(sqrt1.y, y) annotation (Line(
        points={{1,0},{110,0}},
        color={0,0,127},
        smooth=Smooth.None));
    annotation (Documentation(revisions="<html>
<p>
<table border=1 cellspacing=0 cellpadding=2>
<tr><th>Date</th> <th align=\"left\">Description</th></tr>

<tr><td valign=\"top\"> June 22, 2015 </td>
    <td valign=\"top\"> 

<table border=0>
<tr><td valign=\"top\">
         <img src=\"modelica://Modelica_Noise/Resources/Images/Blocks/Noise/dlr_logo.png\">
</td><td valign=\"bottom\"> 
         Initial version implemented by
         A. Kl&ouml;ckner, F. v.d. Linden, D. Zimmer, M. Otter.<br>
         <a href=\"http://www.dlr.de/rmc/sr/en\">DLR Institute of System Dynamics and Control</a>
</td></tr></table>
</td></tr>

</table>
</p>
</html>",                                   info="<html>
<p>This block calculates the standard deviation of its input signal. The standard deviation is the square root of the signal&apos;s variance:</p>
<blockquote>
<pre>y = sqrt( variance(u) )</pre>
</blockquote>
<p>
The <a href=\"modelica://Modelica_Noise.Blocks.Statistics.Variance\">Variance</a> block is used to 
calculate variance(u).
</p>
<p>The parameter t_eps is used to guard against division by zero (the computation of the standard deviation
starts at startTime + t_eps and before that time instant y = 0).
</p>

<p>
This block is demonstrated in the examples
<a href=\"modelica://Modelica_Noise.Blocks.Examples.NoiseExamples.UniformNoiseProperties\">UniformNoiseProperties</a>,
<a href=\"modelica://Modelica_Noise.Blocks.Examples.NoiseExamples.NormalNoiseProperties\">NormalNoiseProperties</a> and
<a href=\"modelica://Modelica_Noise.Blocks.Examples.NoiseExamples.WeibullNoiseProperties\">WeibullNoiseProperties</a>.
</p>
</html>"),
      Diagram(coordinateSystem(preserveAspectRatio=false, extent={{-100,-100},{100,
              100}}), graphics),
      Icon(graphics={
          Line(points={{-76,68},{-76,-80}}, color={192,192,192}),
          Line(points={{-86,0},{72,0}}, color={192,192,192}),
          Line(
             points={{-76,-13},{-62,-13},{-62,3},{-54,3},{-54,-45},{-46,-45},{-46,
                -23},{-38,-23},{-38,61},{-30,61},{-30,29},{-30,29},{-30,-31},{-20,
                -31},{-20,-13},{-10,-13},{-10,-41},{0,-41},{0,41},{6,41},{6,55},
                {12,55},{12,-1},{22,-1},{22,11},{28,11},{28,-19},{38,-19},{38,53},
                {48,53},{48,19},{56,19},{56,-47},{66,-47}},
              color={215,215,215}),
          Polygon(
            points={{94,0},{72,8},{72,-8},{94,0}},
            lineColor={192,192,192},
            fillColor={192,192,192},
            fillPattern=FillPattern.Solid),
          Line(
            points={{-76,46},{70,46}},
            color={215,215,215},
            smooth=Smooth.None),
          Line(
            points={{-16,0},{-16,30}},
            color={0,0,0},
            smooth=Smooth.None),
          Polygon(
            points={{-76,90},{-84,68},{-68,68},{-76,90}},
            lineColor={192,192,192},
            fillColor={192,192,192},
            fillPattern=FillPattern.Solid),
          Polygon(
            points={{-16,46},{-24,24},{-8,24},{-16,46}},
            lineColor={0,0,0},
            fillColor={0,0,0},
            fillPattern=FillPattern.Solid)}));
  end StandardDeviation;
  annotation (Icon(graphics={Line(
          points={{-72,-63.953},{-68.5,-63.8975},{-65,-63.7852},{-61.5,
          -63.5674},{-58,-63.1631},{-54.5,-62.4442},{-51,-61.2213},{-47.5,
          -59.2318},{-44,-56.1385},{-40.5,-51.5468},{-37,-45.0467},{-33.5,
          -36.2849},{-30,-25.0617},{-26.5,-11.4388},{-23,4.1682},{-19.5,
          20.9428},{-16,37.695},{-12.5,52.9771},{-9,65.2797},{-5.5,
          73.2739},{-2,76.047},{1.5,73.2739},{5,65.2797},{8.5,52.9771},{
          12,37.695},{15.5,20.9428},{19,4.1682},{22.5,-11.4388},{26,
          -25.0617},{29.5,-36.2849},{33,-45.0467},{36.5,-51.5468},{40,
          -56.1385},{43.5,-59.2318},{47,-61.2213},{50.5,-62.4442},{54,
          -63.1631},{57.5,-63.5674},{61,-63.7852},{64.5,-63.8975},{68,
          -63.953}},
          color={0,0,0},
          smooth=Smooth.Bezier)}), Documentation(info="<html>
</html>", revisions="<html>
<p>
<table border=1 cellspacing=0 cellpadding=2>
<tr><th>Date</th> <th align=\"left\">Description</th></tr>

<tr><td valign=\"top\"> June 22, 2015 </td>
    <td valign=\"top\"> 

<table border=0>
<tr><td valign=\"top\">
         <img src=\"modelica://Modelica_Noise/Resources/Images/Blocks/Noise/dlr_logo.png\">
</td><td valign=\"bottom\"> 
         Initial version implemented by
         A. Kl&ouml;ckner, F. v.d. Linden, D. Zimmer, M. Otter.<br>
         <a href=\"http://www.dlr.de/rmc/sr/en\">DLR Institute of System Dynamics and Control</a>
</td></tr></table>
</td></tr>

</table>
</p>
</html>"));
end Math;
